# Exercises for Chapter 2.2

**Exercise 2.20.**

Use [dotted-tail] notation to write a procedure `same-parity` that takes one or
more integers and returns a list of all the arguments that have the same even-odd
parity as the first argument. For example,

```
(same-parity 1 2 3 4 5 6 7)
(1 3 5 7)

(same-parity 2 3 4 5 6 7)
(2 4 6)
```

**Answer 2.20.**

```scheme
(define (same-parity a . z)
  (define (parity-list con? res lst)
    (cond ((null? lst) res)
          ((con? (car lst))
            (parity-list con? (append res (list (car lst))) (cdr lst)))
          (else
            (parity-list con? res (cdr lst)))))
  (if (even? a)
      (parity-list even? (list a) z)
      (parity-list odd? (list a) z)))
```

-----

**Exercise 2.21.**

The procedure `for-each` is similar to `map`. It takes as arguments a procedure and
a list of elements. However, rather than forming a list of the results,
`for-each` just applies the procedure to each of the elements in turn, from left
to right. The values returned by applying the procedure to the elements are not
used at all -- `for-each` is used with procedures that perform an action, such as
printing. For example,

```scheme
(for-each (lambda (x) (newline) (display x))
          (list 57 321 88))
57
321
88 
```

The value returned by the call to `for-each` (not illustrated above) can be
something arbitrary, such as `true`. Give an implementation of `for-each`.

**Answer 2.21.**

One way to implement `for-each` is to recurse progressively down the list:

```scheme
(define (for-each f lst)
  ;; Apply a function `f` to every item in the list `lst`, returning `true`
  (cond ((null? lst) #t)
        (else
          (f (car lst))
          (for-each f (cdr lst)))))
```

If we don't care at all about the return value, however, using `map` will
also work just fine:

```scheme
(define (for-each-map f lst)
  ;; Apply a function `f` to every item in the list `lst`, returning a list
  ;; of undefined elements with the same length as `lst`
  (map f lst))
```

-----

**Exercise 2.26.**

Suppose we define x and y to be two lists:

```scheme
(define x (list 1 2 3))
(define y (list 4 5 6))
```

What result is printed by the interpreter in response to evaluating each of the
following expressions:

```scheme
(append x y)

(cons x y)

(list x y)
```

**Answer 2.26.**

```scheme
(append x y)
> (1 2 3 4 5 6)

(cons x y)
> ((1 2 3) 4 5 6)

(list x y)
> ((1 2 3) (4 5 6))
```

-----

**Exercise 2.28.**

Write a procedure `fringe` that takes as argument a tree (represented as a list)
and returns a list whose elements are all the leaves of the tree arranged in
left-to-right order. For example,

```scheme
(define x (list (list 1 2) (list 3 4)))

(fringe x)
(1 2 3 4)

(fringe (list x x))
(1 2 3 4 1 2 3 4)
```

**Answer 2.28.**

```scheme
(define (fringe tree)
  ;;; Return a flattened version of the list `tree` (all the leaves arranged in
  ;;; left-to-right order)
  (define (flatten-tree res lst)
    (if (null? lst)
        res
        (let ((elem (car lst)))
          (if (list? elem)
              (flatten-tree (flatten-tree res elem)
                            (cdr lst))
              (flatten-tree (append res (list elem))
                            (cdr lst))))))
  (flatten-tree (list) tree))
```
