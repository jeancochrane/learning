# Exercises for Section 1.3

**Exercise 1.29.**

Simpson's Rule is a more accurate method of numerical integration than the
method illustrated above. Using Simpson's Rule, the integral of a function
`f` between `a` and `b` is approximated as:

```
(h/3) * (y_0 + 4y_1 + 2y_2 + 4y_3 + 2y_4 + ... + 2y_n-2 + 4y_n-1 + y_n)
```

where `h = (b - a)/n`, for some even integer `n`, and `y_k = f(a + kh)`. (Increasing
`n` increases the accuracy of the approximation.) Define a procedure that takes
as arguments `f`, `a`, `b`, and `n` and returns the value of the integral, computed
using Simpson's Rule. Use your procedure to integrate `cube` between 0 and
1 (with n = 100 and n = 1000), and compare the results to those of the integral
procedure shown above.

**Answer 1.29.** 

Here's a procedure to compute a definite integral using Simpson's Rule:

```scheme
(define (h a b n)
  ;; `h` as defined in Simpson's rule
  (/ (- b a) n))

(define (yk f a b n k)
  ;; The incremental function evaluation in the sum term
  ;; of Simpson's rule
  (f (+ a (* k (h a b n)))))

(define (coef k)
  ;; The coefficient of yk in the sum term of Simpson's rule
  (cond ((= 0 k) 1)
        ((even? k) 4)
        (else 2)))

(define (simpsons-sum f a b n k)
  ;; The sum term of Simpson's rule
  (if (= k n)
      (yk f a b n k)
      (+ (* (coef k) (yk f a b n k))
         (simpsons-sum f a b n (+ k 1)))))

(define (simpsons-rule f a b n)
  ;; Compute the definite integral of `f` between `a` and `b`
  ;; to a resolution of `n`
  (* (/ (h a b n) 3) (simpsons-sum f a b n 0)))
```

And here's the integral of `cube` between 0 and 1 at various resolutions:

```
> (simpsons-rule cube 0 1 100)
0.24671666666666667

> (simpsons-rule cube 0 1 1000)
0.24966716666666666
```

_TK: comparison with the method in the text._

-----

**Exercise 1.30.**

The `sum` procedure above generates a linear recursion. The procedure can be
rewritten so that the sum is performed iteratively. Show how to do this by
filling in the missing expressions in the following definition:

```scheme
(define (sum term a next b)
  (define (iter a result)
    (if <??>
        <??>
        (iter <??> <??>)))
  (iter <??> <??>))
```

**Answer 1.30.**

```scheme
(define (sum term a next b)
  ;; An iterative version of the generic summation procedure.
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (+ (term a) result))))
  (iter a 0))
```

-----

**Exercise 1.31.a**

The `sum` procedure is only the simplest of a vast number of similar abstractions
that can be captured as higher-order procedures. Write an analogous procedure
called `product` that returns the product of the values of a function at points
over a given range. Show how to define `factorial` in terms of `product`. Also use
`product` to compute approximations to `pi` using the formula:

```
(pi / 4) = (2 * 4 * 4 * 6 * 6 * 8 ...) / (3 * 3 * 5 * 5 * 7 * 7 ...)
```

**Answer 1.31.a**

Procedure definitions:

```scheme
(define (product term a next b)
  ;; A higher-order procedure to generate product procedures
  (if (> a b)
      1
      (* (term a) (product term (next a) next b))))

(define (factorial n)
  ;; The factorial function defined in terms of `product`
  (define (term n) n)
  (define (next n) (+ n 1))
  (product term 1 next n))

(define (pi-approx n)
  ;; Approximate pi to the resolution `n`
  (define (term x)
    (if (even? x)
        (/ (+ 2 x) (+ 1 x))
        (/ (+ 1 x) (+ 2 x))))
  (define (next x) (+ 1 x))
  (* 4 (product term 1 next n)))
```

Approximations of `pi`:

```
> (pi-approx 10)
3.2751010413348074

> (pi-approx 100)
3.1570301764551676

> (pi-approx 1000)
3.1431607055322663
```

**Exercise 1.31.b**

If your `product` procedure generates a recursive process, write one that
generates an iterative process. If it generates an iterative process, write one
that generates a recursive process.

**Answer 1.31.b**

```scheme
(define (product-iter term a next b)
  ;; An iterative version of `product`
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (* (term a) result))))
  (iter a 1))
```

-----

**Exercise 1.32.a.**

Show that `sum` and `product` (exercise 1.31) are both special cases of a still
more general notion called `accumulate` that combines a collection of terms,
using some general accumulation function:

```
(accumulate combiner null-value term a next b)
```

`accumulate` takes as arguments the same term and range specifications as `sum` and
`product`, together with a combiner procedure (of two arguments) that specifies
how the current term is to be combined with the accumulation of the preceding
terms and a null-value that specifies what base value to use when the terms run
out. Write `accumulate` and show how `sum` and `product` can both be defined as
simple calls to `accumulate`.

**Answer 1.32.a.**

```scheme
(define (accumulate combiner null-value term a next b)
  ;; A higher-order procedure to generate "accumulation" procedures
  ;; (Like `product` and `sum`
  (if (> a b)
      null-value
      (combiner (term a)
                (accumulate combiner null-value term (next a) next b))))
```

Redefining `sum` and `product` using `accumulate` is simple:

```scheme
(define (sum a b)
  (define (combiner a b) (+ a b))
  (define null-value 0)
  (define (term n) n)
  (define (next n) (+ 1 n))
  (accumulate combiner null-value term a next b))

(define (product a b)
  (define (combiner a b) (* a b))
  (define null-value 1)
  (define (term n) n)
  (define (next n) (+ 1 n))
  (accumulate combiner null-value term a next b))
```


**Exercise 1.32.b.**

If your `accumulate` procedure generates a recursive process, write one that
generates an iterative process. If it generates an iterative process, write one
that generates a recursive process.

**Answer 1.32.b.**

```
(define (accumulate-iter combiner null-value term a next b)
  ;; Iterative version of `accumulate` 
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (combiner (term a) result))))
  (iter a null-value))
```

-----

**Exercise 1.33.**

You can obtain an even more general version of `accumulate` (exercise 1.32) by
introducing the notion of a filter on the terms to be combined. That is,
combine only those terms derived from values in the range that satisfy
a specified condition. The resulting `filtered-accumulate` abstraction takes the
same arguments as `accumulate`, together with an additional predicate of one
argument that specifies the filter. Write `filtered-accumulate` as a procedure.

```scheme
(define (filtered-accumulate combiner null-value filter term a next b)
  ;; Higher-order abstraction to generate accumulation procedures
  ;; (Like sum, product, sum-of-squares)
  (cond ((> a b) null-value)
        ((filter a)
         (combiner (term a)
                (filtered-accumulate combiner null-value filter term (next a) next b)))
        (else (filtered-accumulate combiner null-value filter term (next a) next b))))
```

**Exercise 1.33.a.**

Show how to express the sum of the squares of the prime numbers in the interval
`a` to `b` (assuming that you have a `prime?` predicate already written) using
`filtered-accumulate`.

```scheme
(define (sum-of-prime-squares a b)
  ;; Sum the squares of the prime series using `filtered-accumulate`
  (define (filter n) (prime? n))
  (define (combiner a b) (+ a b))
  (define null-value 0)
  (define (term n) (* n n))
  (define (next n) (+ 1 n))
  (filtered-accumulate combiner null-value filter term a next b))
```

**Exercise 1.33.a.**

Show how to express the product of all the positive integers less than `n` that
are relatively prime to `n` (i.e., all positive integers `i < n` such that `GCD(i,n)
= 1`) using `filtered-accumulate`.

```scheme
(define (product-of-primes n)
  ;; Find the product of all positive integers less than `n` that
  ;; are relatively prime to `n` (i.e., all positive integers `i < n`
  ;; such that `GCD(i,n) = 1`)
  (define (relative-prime? a)
    (= (gcd a n) 1))
  (define (filter a) (relative-prime? a))
  (define (combiner a b) (* a b))
  (define null-value 1)
  (define (term a) a)
  (define (next a) (+ 1 a))
  (filtered-accumulate combiner null-value filter term 1 next n))
```

-----

**Exercise 1.34**

Suppose we define the procedure

```scheme
(define (f g)
  (g 2))
```

Then we have

```
> (f square)
4

> (f (lambda (z) (* z (+ z 1))))
6
```

What happens if we (perversely) ask the interpreter to evaluate the combination
`(f f)`? Explain.

**Answer 1.34**

The interpreter will substitute `f` for `g`, leading to the following
nonsensical expression:

```
(f f)
(f 2)
(2 2)
```

Since `2` is an integer literal and not a valid procedure, the interpreter
will throw an error. My interpreter (`mit-scheme`) returns the error message `The object 2
is not applicable`, but your mileage may vary.

-----

**Exercise 1.35.**

Show that the golden ratio `phi` (section 1.2.2) is a fixed point of the
transformation `x -> 1 + 1/x`, and use this fact to compute `phi` by means of
the `fixed-point` procedure. 

**Answer 1.35.**

To confirm that `phi` is indeed a fixed point of `x -> 1 + 1/x`,
let's first simplify the transformation:

```
x = 1 + 1/x
x = (x + 1) / x
x^2 = x + 1
x^2 - x - 1 = 0
```

Using Scheme, we can substitute in the Golden Ratio and confirm
that `phi` does indeed satisfy the equation:

```scheme
(let ((phi (/ (+ 1 (sqrt 5)) 2)))
     (- (square phi) phi 1)))
> 0
```

Success! Now, here's a definition of `fixed-point`, slightly modified from the
version in the chapter to use `let` to set the tolerance:

```scheme
(define (fixed-point f first-guess)
  ;; Compute the fixed point of a function `f` starting with the
  ;; initial value `first-guess`
  (define (close-enough? a b)
    (let ((tolerance 0.001))
        (< (abs (- a b)) tolerance)))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
          guess
          (try next))))
  (try first-guess))
```

Using `fixed-point`, we can easily compute the golden ratio:

```
(fixed-point (lambda (x) (+ 1 (/ 1 x))) 1.0)
> 1.6179775280898876
```

Section 1.2.2 reports the golden ratio to be approximately `1.6180`,
so our estimate is indeed within 0.0001 of the true value.

-----

**Exercise 1.36.**

Modify `fixed-point` so that it prints the sequence of approximations it
generates, using the `newline` and `display` primitives shown in exercise 1.22.
Then find a solution to `x^x = 1000` by finding a fixed point of
`x -> log(1000)/log(x)`. (Use Scheme's primitive `log` procedure, which computes
natural logarithms.) Compare the number of steps this takes with and without
average damping. (Note that you cannot start `fixed-point` with a guess of 1, as
this would cause division by `log(1) = 0`.

**Answer 1.36.**

Here's an altered version of `fixed-point` that prints its
guesses:

```scheme
(define (fixed-point f first-guess)
  ;; Compute the fixed point of a function `f` starting with the
  ;; initial value `first-guess`
  (define (close-enough? a b)
    (let ((tolerance 0.001))
        (< (abs (- a b)) tolerance)))
  (define (try guess)
    (let ((next (f guess)))
      (newline)
      (display guess)
      (if (close-enough? guess next)
          guess
          (try next))))
  (try first-guess))
```

We can use this procedure to watch as Scheme evaluates successive
approximations to `x^x = 1000`:

```
(fixed-point (lambda (x) (/ (log 1000) (log x))) 2.0)
>2.
>9.965784284662087
>3.004472209841214
>6.279195757507157
>3.759850702401539
>5.215843784925895
>4.182207192401397
>4.8277650983445906
>4.387593384662677
>4.671250085763899
>4.481403616895052
>4.6053657460929
>4.5230849678718865
>4.577114682047341
>4.541382480151454
>4.564903245230833
>4.549372679303342
>4.559606491913287
>4.552853875788271
>4.557305529748263
>4.554369064436181
>4.556305311532999
>4.555028263573554
```

Not bad performance! But `average-damp` will do better. Let's define
it for the interpreter:

```scheme
(define (average x y) (/ (+ x y) 2))
(define (average-damp f)
  (lambda (x) (average x (f x))))
```

Now, we can watch as Scheme uses `average-damp` to evaluate approximations even
faster:

```
(fixed-point (average-damp (lambda (x) (/ (log 1000) (log x)))) 2.0)
>2.
>5.9828921423310435
>4.922168721308343
>4.628224318195455
>4.568346513136242
>4.5577305909237005
```

-----

**Exercise 1.41.**

Define a procedure `double` that takes a procedure of one argument as argument
and returns a procedure that applies the original procedure twice. For example,
if `inc` is a procedure that adds 1 to its argument, then `(double inc)` should be
a procedure that adds 2. What value is returned by:

```scheme
(((double (double double)) inc) 5)
```

**Answer 1.41.**

With `lambda`, defining `double` is a snap: 

```scheme
(define (double f)
  ;; Compose a function with itself (that is, 'double' it)
  (lambda (x) (f (f x))))
```

Now, let's evaluate the expression:

```
(((double (double double)) inc) 5)
> 21
```

(double double)
(lambda (x) (double (double x)))

We can validate this result theoretically:

```scheme
(((double (double double)) inc) 5)
(((double (lambda (x) (double (double x)))) inc) 5)
((double (double (double (double inc)))) 5)
...
```

Since this is four doublings, we'll compose `inc` with itself
`2^4 = 16` times, meaning we anticipate the result `5 + 16 =21`.

-----

**Exercise 1.42.**

Let `f` and `g` be two one-argument functions. The composition `f` after `g` is defined
to be the function `x -> f(g(x))`. Define a procedure `compose` that implements
composition. For example, if `inc` is a procedure that adds 1 to its argument:

```scheme
((compose square inc) 6)
> 49
```

**Answer 1.42.**

`compose` is a trivial modification of `double` -- or rather, `double` is
a particular case of `compose`! In either case, the procedure definition is
trivial:

```scheme
(define (compose f g)
  ;; Compose a function `f` with a function `g`, returning the composition
  ;; `x -> f(g(x))`
  (lambda (x) (f (g x))))
```

-----

**Exercise 1.43.**

If `f` is a numerical function and `n` is a positive integer, then we can form the
`n`th repeated application of `f`, which is defined to be the function whose value
at `x` is `f(f(...(f(x))...))`. For example, if `f` is the function `x -> x + 1`, then the
`n`th repeated application of `f` is the function `x -> x + n`. If `f` is the operation of
squaring a number, then the `n`th repeated application of `f` is the function that
raises its argument to the `2^n`th power. Write a procedure that takes as inputs
a procedure that computes `f` and a positive integer `n` and returns the procedure
that computes the `n`th repeated application of `f`. Your procedure should be able
to be used as follows:

```scheme
((repeated square 2) 5)
> 625
```

**Answer 1.43.**

```scheme
(define (repeated f n)
  ;; Return a procedure that computes the `n`th repeated application
  ;; of a function `f`
  (if (= n 1)
      f
      (repeated (compose f f) (- n 1))))
```

-----

**Exercise 1.44.**

The idea of smoothing a function is an important concept in signal processing.
If `f` is a function and `dx` is some small number, then the smoothed version of
`f` is the function whose value at a point `x` is the average of `f(x - dx)`, `f(x)`,
and `f(x + dx)`. Write a procedure smooth that takes as input a procedure that
computes `f` and returns a procedure that computes the smoothed `f`. It is
sometimes valuable to repeatedly smooth a function (that is, smooth the
smoothed function, and so on) to obtained the `n`-fold smoothed function. Show
how to generate the `n`-fold smoothed function of any given function using `smooth`
and `repeated` from exercise 1.43.

**Answer 1.44.**

```scheme
(define (smooth f)
  ;; Smooth a function `f` using a particular tolerance `dx`
  (let ((dx 0.0001))
       (lambda (x) (average (f (- x dx))
                            (f x)
                            (f (+ x dx))))))

(define (nfold-smooth f n)
  ;; Smooth a function `f`, `n` times
  ((repeated smooth n) f))
```
